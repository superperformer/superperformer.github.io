# Plot the Enhanced Volume in ThinkorSwim
# Written by: @Diamond_Stocks @RayTL_ - TLScripts
# Site:     https://www.traderlion.com/ https://www.traderlion.com/tl-scripts/
# V1

declare on_volume;
declare zerobase;

#Turn on or off Alerts when cycling charts
input Alerts_On = yes; #Hint Alerts_On: Set to "Yes" to receive sound alerts when HRV/LRV Conditions are met.

input ShowTodayVolume =  yes;  #Hint ShowTodayVolume: Show Today's Volume as a Label

#Logic for High Relative Volume
def Length = 8; #was  10
#shorter length down
def length2 = 5;
#Define Down Days
def isDown = ((close[1] > close) and ((high + low) / 2 > close));
#down with goodclose
def isDownGc = ((close[1] > close) and (close >= (high * 0.48 + low * 0.52) ));#2% tlr
#upside reversal
def isUpRev = close > high * 0.75 + low * 0.25 #close at upper 4th
#open lower than prev lower 3rd but close higher, close inside or above
and ((open < high * 0.25 + low * 0.75 and open < low[1] * 2 / 3 + high[1] * 1 / 3 and close > low[1] )
    #open gap down but close mid above
    or (open < low[1] and close > high[1] * 0.5 + low[1] * 0.5)
);
#thick hammer open and close at upper 3rd, close in side
#open > close, open > range 0.6
def isHammer = (open > high * 0.7 + low * 0.3 or close > open and open > high * 0.6 + low * 0.4)
  and close > high * 0.7 + low * 0.3
  and close >= low[1] and close <= high[1];
def isDoji = (AbsValue(close - open) / open <= 0.005) && (close > (high * 1 / 3 + low * 2 / 3));
#Define Up Days
def Up = ((close - close[1] >= 0) and (close >= (high + low) / 2));
#Define Down Volume
def downvolume = (volume * isDown);
#Define Up Volume
def upvolume = (volume * Up);
#Highest Up/Down Volume in Past 10 Days
def HighestDown = Highest(downvolume, Length);
def HighestDown2 = Highest(downvolume, length2);
def HighestUp = Highest(upvolume, Length);

#Lowest Volume in 10 Days
def LowestVolume = Lowest(volume, Length);

plot HRV = if (upvolume > HighestDown) then 1 else 0;
plot HRV2 = if (upvolume > HighestDown2) then 1 else 0;
plot LRV = if (volume <= LowestVolume) then 1 else 0;

plot Vol = volume;
plot avgVol = Average(volume, 30);

avgVol.AssignValueColor(Color.YELLOW);
Vol.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
Vol.SetLineWeight(4);
Vol.DefineColor("HRV", CreateColor(0, 230, 118));#pp
Vol.DefineColor("HRV2", CreateColor(1, 145, 255));#pp5
Vol.DefineColor("LRV", CreateColor(255, 165, 0)); #dry
Vol.DefineColor("Up", CreateColor(13, 83, 78)); #was (33, 150, 243));
Vol.DefineColor("Down", CreateColor(121, 42, 40)); #was (224, 64, 251));
Vol.DefineColor("DownGc", CreateColor(175, 122, 197));
Vol.DefineColor("Hammer", CreateColor(160, 215, 43)); #dark yellow green
Vol.DefineColor("UpRev", CreateColor(0, 255, 255));# cyan

#Vol.AssignValueColor(if HRV == 1 then Vol.Color("HRV") else if LRV == 1 then Vol.Color("LRV") else if ((close - close[1] >= 0) and (close >= (high + low) / 2)) then Vol.Color("Up") else  Vol.Color("isDown"));
#use simple up/down color
Vol.AssignValueColor(if HRV == 1 then Vol.Color("HRV")
    else if HRV2 == 1 then Vol.Color("HRV2")
    else if isHammer == 1 then Vol.Color("Hammer")
    else if isUpRev == 1 then Vol.Color("UpRev")
    else if LRV == 1 then Vol.Color("LRV")
    else if ((close >= close[1]) and (close >= (high * 1 / 3 + low * 2 / 3))) then Vol.Color("Up")
    else if isDownGc == 1 then Vol.Color("DownGc")
    else if isDoji == 1 then Vol.Color("DownGc")
    else  Vol.Color("Down"));

#get highest volume on current chart timeframe
def highestAllTime = if volume == HighestAll(volume) then yes else Double.NaN;

#Add chart bubble above highest bar
AddChartBubble(highestAllTime, HighestAll(volume), HighestAll(volume),  Color.WHITE, yes);



#Add Label to Show Current Day Volume
AddLabel(ShowTodayVolume, "Volume Today: " + volume, if ((close - close[1] >= 0) and (close >= (high + low) / 2)) then Vol.Color("Up") else if close <= open then Vol.Color("Down") else Color.GRAY);

#Alert Capability
Alert( if Alerts_On and HRV then yes else no, "High Relative Volume", Alert.BAR, Sound.Ding);
Alert(if Alerts_On and LRV then yes else no, "Low Relative Volume", Alert.BAR, Sound.Chimes);